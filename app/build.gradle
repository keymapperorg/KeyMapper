apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "kotlinx-serialization"

android {
    compileSdkVersion 30
    buildToolsVersion = '30.0.3'

    def props = new Properties()
    file("version.properties").withInputStream { props.load(it) }

    defaultConfig {
        applicationId "io.github.sds100.keymapper"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode props.getProperty("VERSION_CODE").toInteger()
        versionName props.getProperty("VERSION_NAME")
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        ci {
            debuggable = true

            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles 'proguard-rules.pro'
            }

            /*
            This is required because the splitties library doesn't have a ci build type.
             */
            matchingFallbacks = ['debug']

            applicationIdSuffix ".ci"
            versionNameSuffix "-ci." + props.getProperty("VERSION_NUM")
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // work-runtime-ktx 2.1.0 and above now requires Java 8
    kotlinOptions {
        jvmTarget = "1.8"
    }

    kapt {
        correctErrorTypes = true
    }

    kotlinOptions.useIR = false
}

android.sourceSets {
    test {
        java.srcDirs += "$projectDir/src/testShared"
    }

    androidTest {
        java.srcDirs += "$projectDir/src/testShared"
        assets.srcDirs += files("$projectDir/schemas".toString())
        resources.srcDirs += ['src/test/resources']
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "keymapper-${variant.versionName}.apk"
    }
}

dependencies {
    def room_version = "2.3.0"
    def coroutinesVersion = "1.4.1"
    def nav_version = '2.3.5'
    def work_version = "2.5.0"
    def epoxy_version = "4.4.4"
    def splitties_version = "3.0.0-beta01"
    def markwonVersion = "4.3.1"
    def multidex_version = "2.0.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin stuff
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.0"

    // random stuff
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.1.0'
    implementation 'net.lingala.zip4j:zip4j:2.8.0'

    // splitties
    implementation "com.louiscad.splitties:splitties-bitflags:$splitties_version"
    implementation "com.louiscad.splitties:splitties-alertdialog-appcompat-coroutines:$splitties_version"
    implementation("com.louiscad.splitties:splitties-alertdialog-material:$splitties_version")
    implementation "com.louiscad.splitties:splitties-snackbar:$splitties_version"
    implementation "com.louiscad.splitties:splitties-toast:$splitties_version"
    implementation "com.louiscad.splitties:splitties-mainthread:$splitties_version"

    // androidx
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.core:core-ktx:1.5.0'

    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.multidex:multidex:$multidex_version"
    implementation "androidx.browser:browser:1.3.0"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta02"
    kapt "org.xerial:sqlite-jdbc:3.34.0"
    kapt "androidx.room:room-compiler:$room_version"

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    def junitVersion = "4.13.2"
    def androidXTestExtKotlinRunnerVersion = "1.1.2"
    def espressoVersion = "3.3.0"
    def androidXTestCoreVersion = "1.3.0"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
    testImplementation "androidx.test:core-ktx:1.3.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "pl.pragmatists:JUnitParams:1.1.1"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    testImplementation "org.mockito:mockito-core:3.8.0"
    testImplementation('org.mockito:mockito-inline:3.8.0')

    androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    androidTestImplementation "org.mockito:mockito-android:3.1.0"
    debugImplementation "androidx.fragment:fragment-testing:1.3.5"
    implementation "androidx.test:core:$androidXTestCoreVersion"
}